{
  "hash": "d8a9ee4b39bdd9b9808d5b442d26b3ee",
  "result": {
    "markdown": "---\ntitle: \"The Koch snowflake \"\nauthor: \"Alejandro Morales Sierra\"\ndate: last-modified\nexecute: \n  cache: true\n---\n\nIn this example, we create a Koch snowflake, which is one of the earliest fractals to be described. The Koch snowflake is a closed curve composed on multiple of segments of different lengths. Starting with an equilateral triangle, each segment in the snowflake is replaced by four segments of smaller length arrange in a specific manner. Graphically, the first four iterations of the Koch snowflake construction process result in the following figures (the green segments are shown as guides but they are not part of the snowflake):\n\n![First four iterations fo Koch snowflake fractal](./KochWikipedia.png)\n\nIn order to implement the construction process of a Koch snowflake in VPL we need to understand how a 3D structure can be generated from a graph of nodes. VPL uses a procedural approach to generate of structure based on the concept of turtle graphics.\n\nThe idea behind this approach is to imagine a turtle located in space with a particular position and orientation. The turtle then starts consuming the different nodes in the graph (following its topological structure) and generates 3D structures as defined by the user for each type of node. The consumption of a node may also include instructions to move and/or rotate the turtle, which allows to alter the relative position of the different 3D structures described by a graph.\n\nThe construction process of the Koch snowflake in VPL could then be represented by the following axiom and rewriting rule:\n\naxiom: E(L) + RU(120) + E(L) + RU(120) + E(L)  \nrule:  E(L) → E(L/3) + RU(-60) + E(L/3) + RU(120) + E(L/3) + RU(-60) + E(L/3)\n\nWhere E represent and edge of a given length (given in parenthesis) and RU represents a rotation of the turtle around the upward axis, with angle of rotation given in parenthesis in hexadecimal degrees. The rule can be visualized as follows:\n\n![Koch construction rule](./Koch_order_1.png)\n\nNote that VPL already provides several classes for common turtle movements and rotations, so our implementation of the Koch snowflake only needs to define a class to implement the edges of the snowflake. This can be achieved as follows:\n\n::: {.cell execution_count=1}\n``` {.julia .cell-code}\nusing VPL\nmodule sn\n    import VPL\n    struct E <: VPL.Node\n        length::Float64\n    end\nend\nimport .sn\n```\n:::\n\n\nNote that nodes of type E need to keep track of the length as illustrated in the above. The axiom is straightforward:\n\n::: {.cell execution_count=2}\n``` {.julia .cell-code}\nconst L = 1.0\naxiom = sn.E(L) + VPL.RU(120.0) + sn.E(L) + VPL.RU(120.0) + sn.E(L)\n```\n\n::: {.cell-output-display execution_count=3}\n```\nVPL.Core.StaticGraph(Dict{Int64, Any}(5 => VPL.Core.GraphNode{Main.sn.E}(Main.sn.E(1.0), Set{Int64}(), 4, 5), 4 => VPL.Core.GraphNode{VPL.Geom.RU{Float64}}(VPL.Geom.RU{Float64}(120.0), Set([5]), 3, 4), 2 => VPL.Core.GraphNode{VPL.Geom.RU{Float64}}(VPL.Geom.RU{Float64}(120.0), Set([3]), 1, 2), 3 => VPL.Core.GraphNode{Main.sn.E}(Main.sn.E(1.0), Set([4]), 2, 3), 1 => VPL.Core.GraphNode{Main.sn.E}(Main.sn.E(1.0), Set([2]), missing, 1)), Dict{DataType, Set{Int64}}(Main.sn.E => Set([5, 3, 1]), VPL.Geom.RU{Float64} => Set([4, 2])), 1, 5)\n```\n:::\n:::\n\n\nThe rule is also straightforward to implement as all the nodes of type E will be replaced in each iteration. However, we need to ensure that the length of the new edges is a calculated from the length of the edge being replaced. In order to extract the data stored in the node being replaced we can simply use the function data. In this case, the replacement function is defined and then added to the rule. This can make the code more readable but helps debugging and testing the replacement function.\n\n::: {.cell execution_count=3}\n``` {.julia .cell-code}\nfunction Kochsnowflake(x)\n    L = data(x).length\n    sn.E(L/3) + RU(-60.0) + sn.E(L/3) + RU(120.0) + sn.E(L/3) + RU(-60.0) + sn.E(L/3)\n end\n rule = Rule(sn.E, rhs = Kochsnowflake)\n```\n\n::: {.cell-output-display execution_count=4}\n```\nRule replacing nodes of type Main.sn.E without context capturing.\n```\n:::\n:::\n\n\nThe model is then created by constructing the graph\n\n::: {.cell execution_count=4}\n``` {.julia .cell-code}\nKoch = Graph(axiom = axiom, rules = Tuple(rule))\n```\n\n::: {.cell-output-display execution_count=5}\n```\nDynamic graph with 5 nodes of types Main.sn.E,VPL.Geom.RU{Float64} and 1 rewriting rules.\n```\n:::\n:::\n\n\nIn order to be able to generate a 3D structure we need to define a method for the function `VPL.feedgeom!` (notice the need to prefix it with `VPL.` as we are going to define a method for this function). The method needs to two take two arguments, the first one is always an object of type MTurtle and the second is an object of the type for which the method is defined (in this case, E).\n\nThe body of the method should generate the 3D structures using the geometry primitives provided by VPL and feed them to the turtle that is being passed to the method as first argument. In this case, we are going to represent the edges of the Koch snowflakes with cylinders, which can be generated with the `HollowCylinder!` function from VPL. Note that the `feedgeom!` should return `nothing`, the turtle will be modified in place (hence the use of `!` at the end of the function as customary in the VPL community):\n\n::: {.cell execution_count=5}\n``` {.julia .cell-code}\nfunction VPL.feedgeom!(turtle::MTurtle, e::sn.E)\n    HollowCylinder!(turtle, l = e.length, w = e.length/10, h = e.length/10, move = true)\n    return nothing\nend\n```\n:::\n\n\nNote that the argument `move = true` indicates that the turtle should move forward as the cylinder is generated a distance equal to the length of the cylinder.\n\nWith the `feedgeom!` method defined we can already generate the 3D structure, but we cannot render it yet as we also need to define the color with which to render it. This is achieved by defining the `VPL.feedcolor!` method that takes a `GLTurtle` as argument (notice the different data types, `MTurtle` to collect triangular meshes, `GLTurtle` to collect colors for rendering). In this case, we just feed a basic `RGB` color defined by the proportion of red, green and blue. To make the figures more appealing, we can assign random values to each channel of the color to generate random colors.\n\n::: {.cell execution_count=6}\n``` {.julia .cell-code}\nfunction VPL.feedcolor!(turtle::GLTurtle, e::sn.E)\n    feedcolor!(turtle, RGB(rand(), rand(), rand()))\n    return nothing\nend\n```\n:::\n\n\nAfter defining these two methods, we can now call the function render on the graph to generate a 3D interactive image of the Koch snowflake in the current state\n\n::: {.cell execution_count=7}\n``` {.julia .cell-code}\nrender(Koch, axes = false)\n```\n\n::: {.cell-output-display execution_count=8}\n![](index_files\\figure-html/cell-8-output-1.png)\n:::\n:::\n\n\nThis renders the initial triangle of the construction procedure of the Koch snowflake. Let's execute the rules once to verify that we get the 2nd iteration (check the figure at the beginning of this document):\n\n::: {.cell execution_count=8}\n``` {.julia .cell-code}\nrewrite!(Koch)\nrender(Koch, axes = false)\n```\n\n::: {.cell-output-display execution_count=9}\n![](index_files\\figure-html/cell-9-output-1.png)\n:::\n:::\n\n\nAnd two more times\n\n::: {.cell execution_count=9}\n``` {.julia .cell-code}\nfor i in 1:3\n    rewrite!(Koch)\nend\nrender(Koch, axes = false)\n```\n\n::: {.cell-output-display execution_count=10}\n![](index_files\\figure-html/cell-10-output-1.png)\n:::\n:::\n\n\n# Other snowflake fractals\n\nTo demonstrate the power of this approach, let's create an alternative snowflake. We will simply invert the rotations of the turtle in the rewriting rule\n\n::: {.cell execution_count=10}\n``` {.julia .cell-code}\nfunction Kochsnowflake2(x)\n   L = data(x).length\n   sn.E(L/3) + RU(60.0) + sn.E(L/3) + RU(-120.0) + sn.E(L/3) + RU(60.0) + sn.E(L/3)\nend\nrule2 = Rule(sn.E, rhs = Kochsnowflake2)\nKoch2 = Graph(axiom = axiom, rules = Tuple(rule2))\n```\n\n::: {.cell-output-display execution_count=11}\n```\nDynamic graph with 5 nodes of types Main.sn.E,VPL.Geom.RU{Float64} and 1 rewriting rules.\n```\n:::\n:::\n\n\nThe axiom is the same, but now the edges added by the rule will generate the edges towards the inside of the initial triangle. Let's execute the first three iterations and render the results\n\n::: {.cell execution_count=11}\n``` {.julia .cell-code}\n# First iteration\nrewrite!(Koch2)\nrender(Koch2, axes = false)\n# Second iteration\nrewrite!(Koch2)\nrender(Koch2, axes = false)\n# Third iteration\nrewrite!(Koch2)\nrender(Koch2, axes = false)\n```\n\n::: {.cell-output-display execution_count=12}\n![](index_files\\figure-html/cell-12-output-1.png)\n:::\n:::\n\n\nThis is know as [Koch antisnowflake](https://mathworld.wolfram.com/KochAntisnowflake.html). We could also easily generate a [Cesàro fractal](https://mathworld.wolfram.com/CesaroFractal.html) by also changing the axiom:\n\n::: {.cell execution_count=12}\n``` {.julia .cell-code}\naxiomCesaro = sn.E(L) + RU(90.0) + sn.E(L) + RU(90.0) + sn.E(L) + RU(90.0) + sn.E(L)\nCesaro = Graph(axiom = axiomCesaro, rules = (rule2,))\nrender(Cesaro, axes = false)\n```\n\n::: {.cell-output-display execution_count=13}\n![](index_files\\figure-html/cell-13-output-1.png)\n:::\n:::\n\n\nAnd, as before, let's go through the first three iterations\n\n::: {.cell execution_count=13}\n``` {.julia .cell-code}\n# First iteration\nrewrite!(Cesaro)\nrender(Cesaro, axes = false)\n# Second iteration\nrewrite!(Cesaro)\nrender(Cesaro, axes = false)\n# Third iteration\nrewrite!(Cesaro)\nrender(Cesaro, axes = false)\n```\n\n::: {.cell-output-display execution_count=14}\n![](index_files\\figure-html/cell-14-output-1.png)\n:::\n:::\n\n\n",
    "supporting": [
      "index_files\\figure-html"
    ],
    "filters": [],
    "includes": {}
  }
}